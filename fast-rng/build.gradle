plugins {
    id 'maven-publish'
    id 'net.researchgate.release' version '2.8.0'
    id 'com.jfrog.bintray' version '1.8.4'
}

repositories {
    maven {
        url 'http://simulation.tudelft.nl/maven/'
    }
}

dependencies {
    testImplementation group: 'org.apache.commons', name: 'commons-math3', version: '3.6'
    testImplementation group: 'ca.umontreal.iro', name: 'ssj', version: '2.5'
    testImplementation(group: 'junit', name: 'junit', version: '4.12') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    testImplementation group: 'org.hamcrest', name: 'java-hamcrest', version: '2.0.0.0'
}

javadoc {
    options.locale = 'en_US'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? bintrayUser : ''
    key = project.hasProperty('bintrayKey') ? bintrayKey : ''
    publications = ['maven']

    pkg {
        repo = 'maven'
        name = 'fast-rng'
        userOrg = 'komiya-atsushi'
        licenses = ['MIT']

        websiteUrl = 'https://github.com/komiya-atsushi/fast-rng-java'
        issueTrackerUrl = 'https://github.com/komiya-atsushi/fast-rng-java/issues'
        vcsUrl = 'https://github.com/komiya-atsushi/fast-rng-java.git'

        version {
            name = project.version
            desc = 'fast-rng: Fast random number generator for various distributions'
        }
    }
}

createReleaseTag.dependsOn bintrayUpload
